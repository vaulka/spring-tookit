ext {
    lombokVersion = "1.18.28"

    // Spring boot
    springBootVersion = "3.1.1"
    // Spring
    springVersion = "6.0.10"
    // Spring cloud
    springCloudVersion = "2022.0.3"
    // Spring 相关依赖管理工具
    springDependencyManagementVersion = "1.1.0"

    // 校验
    // 需要兼容 org.springframework.boot:spring-boot-starter-validation 下的版本
    hibernateValidatorVersion = "8.0.0.Final"

    // 需要兼容 org.springframework.boot:spring-boot-starter-tomcat 下的版本
    // org.apache.tomcat.embed:tomcat-embed-core
    tomcatVersion = "10.1.10"

    // Jackson
    jacksonVersion = "2.15.2"

    // 接口文档
    kinfe4jVersion = "4.1.0"
    swaggerVersion = "2.2.12"
    springdocVersion = "2.1.0"

    // 系统资源监控
    oshiVersion = "6.4.3"
    // MinIO 文件服务器
    minioVersion = "8.5.2"
    // 数据库连接池
    druidVersion = "1.2.18"
    // HTML 解析
    jsoupVersion = "1.16.1"
    // IP 地址解析
    ip2regionVersion = "2.7.0"

    // Apache 工具包
    commonsLang3Version = "3.12.0"
    commonsCodecVersion = "1.16.0"

    // Google 注解
    googleAnnotationsVersion = "3.0.1"

    // Junit
    junitJupiterVersion = "5.9.3"
}

buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath "org.springframework.boot:spring-boot-gradle-plugin:3.1.1"
        classpath "io.spring.gradle:dependency-management-plugin:1.1.0"
    }
}

allprojects {

    group "com.vaulka.kit"
    version "3.1.1.0"

    repositories {
        mavenLocal()
        maven { url "https://maven.aliyun.com/repository/central" }
        maven { url "https://maven.aliyun.com/repository/jcenter" }
        mavenCentral()
    }
}

subprojects {
    if (!name.contains("kit")) {
        apply plugin: "java-library"

        java {
            sourceCompatibility = "17"
        }

        jar {
            enabled = true
            archiveClassifier = ""
        }

        tasks.register("copyJars", Copy) {
            from configurations.runtimeClasspath
            into "$buildDir/libs"
        }

        dependencies {
            compileOnly "org.projectlombok:lombok:$lombokVersion"
            annotationProcessor "org.projectlombok:lombok:$lombokVersion"

            testImplementation "org.projectlombok:lombok:$lombokVersion"
            testAnnotationProcessor "org.projectlombok:lombok:$lombokVersion"
            testImplementation "org.junit.jupiter:junit-jupiter:$junitJupiterVersion"
        }

        test {
            useJUnitPlatform()
        }

    }

    if (!name.contains("kit") && name.contains("spring-boot")) {
        apply plugin: "org.springframework.boot"
        apply plugin: "io.spring.dependency-management"

        bootJar {
            enabled = false
        }

        dependencyManagement {
            imports {
                mavenBom "org.springframework.boot:spring-boot-dependencies:$springBootVersion"
                mavenBom "org.springframework.cloud:spring-cloud-dependencies:$springCloudVersion"
            }
        }

        dependencies {
            api "org.springframework.boot:spring-boot-starter"
            annotationProcessor "org.springframework.boot:spring-boot-configuration-processor"

            testImplementation "org.springframework.boot:spring-boot-starter-test"
        }
    }

}
