ext {
    lombokVersion = "1.18.28"

    // Spring boot
    springBootVersion = "3.1.3"
    // Spring
    springVersion = "6.0.11"
    // Spring cloud
    springCloudVersion = "2022.0.4"

    // 校验
    // validation 需要引入这个包
    classmateVersion = "1.7.0"
    // 需要兼容 org.springframework.boot:spring-boot-starter-validation 下的版本
    hibernateValidatorVersion = "8.0.1.Final"

    // 需要兼容 org.springframework.boot:spring-boot-starter-tomcat 下的版本
    // org.apache.tomcat.embed:tomcat-embed-core
    tomcatVersion = "10.1.12"

    // Jackson
    jacksonVersion = "2.15.2"

    // 接口文档
    knife4jVersion = "4.3.0"
    swaggerVersion = "2.2.15"
    springdocVersion = "2.2.0"

    // 系统资源监控
    oshiVersion = "6.4.3"
    // MinIO 文件服务器
    minioVersion = "8.5.2"
    // 数据库连接池
    druidVersion = "1.2.18"
    // HTML 解析
    jsoupVersion = "1.16.1"
    // IP 地址解析
    ip2regionVersion = "2.7.0"
    // Markdown 转 HTML
    flexmarkAllVersion = "0.64.8"

    // Apache 工具包
    pdfboxVersion = "2.0.29"
    commonsLang3Version = "3.12.0"
    commonsCodecVersion = "1.16.0"

    // Google 注解
    googleAnnotationsVersion = "3.0.1"

    // Junit
    junitJupiterVersion = "5.9.3"
}

buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath "io.spring.gradle:dependency-management-plugin:1.1.2"
        classpath "org.springframework.boot:spring-boot-gradle-plugin:3.1.2"
    }
}

allprojects {

    group "com.vaulka.kit"
    version "3.1.3.1"

    repositories {
        mavenLocal()
        maven { url "https://maven.aliyun.com/repository/central" }
        maven { url "https://maven.aliyun.com/repository/jcenter" }
        mavenCentral()
    }
}

subprojects {
    if (!name.contains("kit")) {
        apply plugin: "java"
        apply plugin: "signing"
        apply plugin: "java-library"
        apply plugin: "maven-publish"

        sourceCompatibility = 17
        targetCompatibility = 17

        jar {
            enabled = true
            archiveClassifier = ""
        }

        tasks.register("copyJars", Copy) {
            from configurations.runtimeClasspath
            into "$buildDir/libs"
        }

        tasks.register("sourcesJar", Jar) {
            from sourceSets.main.allSource
            archiveClassifier.set("sources")
        }

        tasks.register("javadocJar", Jar) {
            from javadoc
            archiveClassifier.set("javadoc")
        }

        dependencies {
            compileOnly "org.projectlombok:lombok:$lombokVersion"
            annotationProcessor "org.projectlombok:lombok:$lombokVersion"

            testImplementation "org.projectlombok:lombok:$lombokVersion"
            testAnnotationProcessor "org.projectlombok:lombok:$lombokVersion"
            testImplementation "org.junit.jupiter:junit-jupiter:$junitJupiterVersion"
        }

        test {
            useJUnitPlatform()
        }

        publishing {
            publications {
                mavenJava(MavenPublication) {
                    groupId = "${group}"
                    artifactId = "${project.name}"
                    version = "${version}"

                    from components.java
                    artifact sourcesJar
                    artifact javadocJar

                    pom {
                        name = "spring-tookit"
                        description = "Spring 增强套件"
                        url = "https://github.com/vaulka/spring-tookit"
                        licenses {
                            license {
                                name = "GPL-v3.0 协议"
                                url = "https://www.gnu.org/licenses/gpl-3.0-standalone.html"
                            }
                        }
                        developers {
                            developer {
                                id = "Vaulka"
                                name = "Vaulka"
                                email = "vaulka7@gmail.com"
                            }
                        }
                        scm {
                            url = "https://github.com/vaulka/spring-tookit"
                        }
                    }

                    versionMapping {
                        usage("java-api") {
                            fromResolutionOf("runtimeClasspath")
                        }
                        usage("java-runtime") {
                            fromResolutionResult()
                        }
                    }
                }
            }

            repositories {
                maven {
                    url "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2"
                    credentials {
                        username ""
                        password ""
                    }
                }
            }
        }

        signing {
            sign publishing.publications.mavenJava
        }

    }

    if (!name.contains("kit") && name.contains("spring-boot")) {
        apply plugin: "org.springframework.boot"
        apply plugin: "io.spring.dependency-management"

        bootJar {
            enabled = false
        }

        dependencyManagement {
            imports {
                mavenBom "org.springframework.boot:spring-boot-dependencies:$springBootVersion"
                mavenBom "org.springframework.cloud:spring-cloud-dependencies:$springCloudVersion"
            }
        }

        dependencies {
            api "org.springframework.boot:spring-boot-starter"
            annotationProcessor "org.springframework.boot:spring-boot-configuration-processor"

            testImplementation "org.springframework.boot:spring-boot-starter-test"
        }
    }

}
